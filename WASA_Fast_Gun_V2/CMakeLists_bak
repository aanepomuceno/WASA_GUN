#----------------------------------------------------------------------------
# Setup the project
cmake_minimum_required(VERSION 3.16...3.21)
project(WASA)

#----------------------------------------------------------------------------
# Find Geant4 package, activating all available UI and Vis drivers by default
# You can set WITH_GEANT4_UIVIS to OFF via the command line or ccmake/cmake-gui
# to build a batch mode only executable
#
option(WITH_GEANT4_UIVIS "Build example with Geant4 UI and Vis drivers" ON)
if(WITH_GEANT4_UIVIS)
  find_package(Geant4 REQUIRED gdml ui_all vis_all)
else()
  find_package(Geant4 REQUIRED gdml)
endif()

#----------------------------------------------------------------------------
# Setup Geant4 include directories and compile definitions
#
include(${Geant4_USE_FILE})

#----------------------------------------------------------------------------
# Locate sources and headers for this project
#
include_directories(${PROJECT_SOURCE_DIR}/include 
                    ${Geant4_INCLUDE_DIR})
file(GLOB sources ${PROJECT_SOURCE_DIR}/src/*.cc)
file(GLOB headers ${PROJECT_SOURCE_DIR}/include/*.hh)

#----------------------------------------------------------------------------
# Add the executable, and link it to the Geant4 libraries
#
add_executable(wasa_main wasa_main.cc ${sources} ${headers})
target_link_libraries(wasa_main ${Geant4_LIBRARIES} )

# MCPL libraries
add_library(mcpl STATIC ${PROJECT_SOURCE_DIR}/src/mcpl.c)
install(TARGETS mcpl DESTINATION lib)
install(FILES mcpl.h DESTINATION include)

add_library(G4MCPLGenerator STATIC ${PROJECT_SOURCE_DIR}/src/G4MCPLGenerator.cc)
install(TARGETS G4MCPLGenerator DESTINATION lib)
install(FILES G4MCPLGenerator.hh DESTINATION include)

add_library(G4MCPLWriter STATIC ${PROJECT_SOURCE_DIR}/src/G4MCPLWriter.cc)
install(TARGETS G4MCPLWriter DESTINATION lib)
install(FILES G4MCPLWriter.hh DESTINATION include)

target_link_libraries(wasa_main mcpl)
target_link_libraries(wasa_main G4MCPLGenerator)
target_link_libraries(wasa_main G4MCPLWriter)

#----------------------------------------------------------------------------
# Copy all scripts to the build directory, i.e. the directory in which we
# build WASA. This is so that we can run the executable directly because it
# relies on these scripts being in the current working directory.
#
set(WASA_SCRIPTS
    wasa_simulation.in vis_wasa.mac
  )

foreach(_script ${WASA_SCRIPTS})
  configure_file(
    ${PROJECT_SOURCE_DIR}/${_script}
    ${PROJECT_BINARY_DIR}/${_script}
    COPYONLY
    )
endforeach()

#----------------------------------------------------------------------------
# Add program to the project targets
# (this avoids the need of typing the program name after make)
#
add_custom_target(WASA DEPENDS wasa_main)

#----------------------------------------------------------------------------
# Install the executable to 'bin' directory under CMAKE_INSTALL_PREFIX
#
install(TARGETS wasa_main DESTINATION bin)

